plugins {
    id 'scala'
    id 'application'
    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'de.mkammerer:argon2-jvm:2.7'
    implementation 'org.scala-lang:scala-library:2.13.2'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.flywaydb:flyway-core'
//    implementation 'org.springframework.session:spring-session-jdbc'
    implementation 'org.scalatra.scalate:scalate-core_2.13:1.9.6'
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.scalatest:scalatest_0.24:3.1.2'
}

mainClassName = 'org.bringo.server.Main'

task compileJs(type: Exec) {
    inputs.files(fileTree("frontend"), "bsconfig.json", "webpack.config.js", "package.json")
    outputs.dir "src/main/resources/public/dist"
    commandLine "npm", "run", "build"
}

task installJs(type: Exec) {
    inputs.files "bsconfig.json", "webpack.config.js", "package.json"
    outputs.dir "node_modules"
    commandLine "npm", "install"
}

task cleanJs(type: Exec) {
    commandLine "npm", "run", "clean"
}

compileJs.dependsOn installJs
compileJava.dependsOn compileJs
clean.dependsOn cleanJs